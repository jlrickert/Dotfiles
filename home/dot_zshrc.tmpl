# Path to your oh-my-zsh installation.

# Set name of the theme to load --- if set to "random", it will
# load a random theme each time oh-my-zsh is loaded, in which case,
# to know which specific one was loaded, run: echo $RANDOM_THEME
# See https://github.com/ohmyzsh/ohmyzsh/wiki/Themes
# ZSH_THEME="bureau"
ZSH_THEME="fino-time"
plugins=(
	ag
	command-not-found
	cp
	debian
	direnv
	docker
	docker-compose
	# fzf
	gh
	git
	golang
	node
	npm
	rust
	vi-mode
	vscode
	z
)

source $ZSH/oh-my-zsh.sh

################################################################################
#                      personalized configuration
################################################################################

_source_if() { [[ -r "$1" ]] && source "$1"; }
_have() { command -v "$1" &>/dev/null; }

{{ if eq .chezmoi.os "darwin" }}
if _have /opt/homebrew/bin/brew; then
	source <(/opt/homebrew/bin/brew shellenv)

	HOMEBREW_PREFIX="$(brew --prefix)"
	if [[ -r "${HOMEBREW_PREFIX}/etc/profile.d/bash_completion.sh" ]]; then
		source "${HOMEBREW_PREFIX}/etc/profile.d/bash_completion.sh"
	else
		for COMPLETION in "${HOMEBREW_PREFIX}/etc/bash_completion.d/"*; do
			[[ -r "${COMPLETION}" ]] && source "${COMPLETION}"
		done
	fi

	FPATH="$(brew --prefix)/share/zsh/site-functions:${FPATH}"

	autoload -Uz compinit
	compinit
fi

# iterm2 integration
test -e "${HOME}/.iterm2_shell_integration.zsh" && source "${HOME}/.iterm2_shell_integration.zsh" || true
{{ end }}

# tabtab source for packages
# uninstall by removing these lines
[[ -f ~/.config/tabtab/zsh/__tabtab.zsh ]] && . ~/.config/tabtab/zsh/__tabtab.zsh || true

# bonzai style completions
owncomp=(keg auth gocomplete chrome kn knp knw ku pomo dlcp qf regx sshkey vic tmpl build win ./build ppm)
for i in "${owncomp[@]}"; do _have "$i" && complete -C "$i" "$i"; done

_have flutter && source <(flutter zsh-completion)
_have podman && source <(podman completion zsh)
_have kubectl && source <(kubectl completion zsh)
_have hugo && source <(hugo completion zsh) && compdef _hugo hugo
_have chezmoi && source <(chezmoi completion zsh)
_have k3s && source <(k3s completion zsh)
_have helm && source <(helm completion zsh)
_have fnm && source <(fnm env --use-on-cd)
_have fzf && source "${XDG_CONFIG_HOME:-$HOME/.config}/fzf/fzf.zsh"

# python argparse completions
_argparse() {
	# This is from the python3-argcomplete package on ubuntu
	# FIXME: make this work on other distros
	if _have register-python-argcomplete3 && _have "$1"; then
		source <(register-python-argcomplete3 "$1")
	fi
}

argcomp=(ansible ansible-config ansible-console ansible-doc ansible-galaxy ansible-inventory ansible-playbook ansible-pull ansible-vault)
for i in "${argcomp[@]}"; do _argparse "$i"; done

# _have nvim && alias vim=nvim
_have ranger && alias r=ranger
_have exa && alias ls='exa --color=auto'
_have exa && alias la='exa -lah'
_have bat && alias cat=bat

#############################################################################
# FZF setup
#############################################################################
# This is the default commands that fzf uses
_fzf_compgen_path() {
	rg --files --hidden --color=never
}

_fzf_compgen_dir() {
	fdfind --color=never --type d --follow --exclude ".git"
}

_fzf_comprun() {
	local command=$1
	shift

	case "$command" in
	cd) fzf --preview 'tree -C {} | head -200' "$@" ;;
	export | unset) fzf --preview "eval 'echo \$'{}" "$@" ;;
	ssh) fzf --preview 'dig {}' "$@" ;;
	keg) ku fzf "$@" ;;
	knp) KEG_CURRENT=~/personal/zettel ku fzf "$@" ;;
	knw) KEG_CURRENT=~/work/zet ku fzf "$@" ;;
	kn) KEG_CURRENT=~/personal/zet ku fzf "$@" ;;
	ku) ku fzf "$@" ;;
	chrome) fzf --ansi --multi "$@" ;;
	vim) fzf --preview 'bat -n --color=always {}' "$@" ;;
	*) fuz "$@" ;;
	esac
}

# TODO: figure out what is going on here.  This feeds an initial list of nodes
# to fzf to prevent garbage displaying at first
_fzf_complete_keg() {
	_fzf_complete +m -- "$@" < <(KEG_CURRENT=$(keg current) ku nodes)
}
_fzf_complete_knp() {
	_fzf_complete +m -- "$@" < <(KEG_CURRENT=~/personal/zettel ku nodes)
}
_fzf_complete_knw() {
	_fzf_complete +m -- "$@" < <(KEG_CURRENT=~/work/zet ku nodes)
}
_fzf_complete_kn() {
	_fzf_complete +m -- "$@" < <(KEG_CURRENT=~/personal/zet ku nodes)
}
_fzf_complete_kn() {
	_fzf_complete +m -- "$@" < <(ku nodes)
}
_fzf_complete_chrome() {
	_fzf_complete +m -- "$@" < <(chrome history)
}
_fzf_complete_vim() {
	_fzf_complete +m -- "$@" < <(rg --files --ignore --color=never)
}
_fzf_complete_vic() {
	_fzf_complete +m -- "$@" < <(vic list)
}
